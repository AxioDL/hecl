# - Config file for the bintoc package
 
# Compute paths
get_filename_component(BINTOC_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
 
# Our library dependencies (contains definitions for IMPORTED targets)
if(NOT TARGET bintoc AND NOT bintoc_BINARY_DIR)
  include("${BINTOC_CMAKE_DIR}/hecl-bintocTargets.cmake")
endif()

macro(bintoc out in sym)
  if(IS_ABSOLUTE ${out})
    set(theOut ${out})
  else()
    set(theOut ${CMAKE_CURRENT_BINARY_DIR}/${out})
  endif()
  if(IS_ABSOLUTE ${in})
    set(theIn ${in})
  else()
    set(theIn ${CMAKE_CURRENT_SOURCE_DIR}/${in})
  endif()
  add_custom_command(OUTPUT ${theOut}
                     COMMAND $<TARGET_FILE:bintoc> ARGS ${theIn} ${theOut} ${sym}
                     DEPENDS ${theIn})
endmacro()
